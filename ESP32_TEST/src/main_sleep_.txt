#include <Arduino.h>
#include "SDM.h"
#include "CAM.hpp"
#include <DHT.h>
#include "RTC.hpp"
#include "GPS.hpp"
#include "BAT.hpp"
#include "SENDWIFI.hpp"

#define RefreshGPS 86400000 // 24h en ms

BAT bat(BAT_PIN);
RTC rtc;
GPS gps(GPS_RX_PIN, GPS_TX_PIN);
CAM cam;
DHT dht(DHT_PIN, DHT22);
SendData send;

volatile int pictureCount = 0;
unsigned int countTime = 0;

void WakeUpLoop() 
{
    digitalWrite(PWR_U_D, HIGH); // Alimentation ON 
    delay(2000); // Attente stabilisation
    initSD();
    cam.begin();

    String fileName = "/pic" + String(pictureCount) + ".jpg";
    cam.takeAndSavePhoto(fileName.c_str()); 

    dht.begin(); // Attend 2 secondes avant de lire les données
    delay(2000);

    float temperature = dht.readTemperature();
    float humidity = dht.readHumidity();
    float batteryPercent = bat.ReadPercent();
    
    if((millis() - countTime) >= RefreshGPS)
    {
        // Mise à Jours GPS et RTC
        Serial.println("Mise à jour GPS");
        countTime = millis();
        while (!gps.gpsready())
        {
            Serial.println("En attente de données GPS...");
            delay(1000);
        }
        gps.update();
        rtc.updateFromGPS(gps.getHours(), gps.getMinutes(), gps.getSeconds(), gps.getDay(), gps.getMonth(), gps.getYear());

    }
    else
    {
        // Mise à jour RTC 
        Serial.println("Mise à jour RTC interne");
        rtc.getDateTime(); 
    }

    // Variable Interne GPS
    String Latitude = gps.getLatitude();
    String Longitude = gps.getLongitude();

    // Variable Interne RTC
    String year = rtc.getYear();
    String month = rtc.getMonth();
    String day = rtc.getDay();
    String hours = rtc.getHours();
    String minutes = rtc.getMinutes();
    String seconds = rtc.getSeconds();
   
    send.SendAllData(fileName, temperature, humidity, Latitude, Longitude, year, month, day, hours, minutes, seconds, batteryPercent);

    pictureCount++;

    digitalWrite(PWR_U_D, LOW); // Alimentation OFF 
}

void mainTask(void *parameter) 
{
    Serial.begin(115200);

    //setCpuFrequencyMhz(40);

    pinMode(PWR_U_D, OUTPUT);
    pinMode(WAKE_UP, INPUT_PULLDOWN);

    // Cause de réveil ?
    esp_sleep_wakeup_cause_t cause = esp_sleep_get_wakeup_cause();

    if (cause == ESP_SLEEP_WAKEUP_EXT0) 
    {
        countTime = millis();

        // Exécute l'action UNE seule fois
        WakeUpLoop();
        
        while (digitalRead(WAKE_UP) == HIGH) {}
    }

    // Réarmer le front montant UNIQUEMENT quand la pin est basse
    esp_sleep_enable_ext0_wakeup((gpio_num_t)WAKE_UP, 1);
    esp_deep_sleep_start();
}


void setup() 
{
    xTaskCreatePinnedToCore(
      mainTask,
      "mainTask",
      4096,
      NULL,
      1,
      NULL,
      0    // CORE 0
    );
}

void loop() 
{
    // Rien ici
}